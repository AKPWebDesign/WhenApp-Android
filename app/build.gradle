plugins {
    id 'com.gladed.androidgitversion' version '0.3.4'
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

androidGitVersion {
    baseCode 1900000000
    codeFormat = 'MNNPPBBB'
    hideBranches = [ ]
    untrackedIsDirty = true
    format = "%tag%%-count%%-commit%%-dirty%"
}

int VERSION_CODE = androidGitVersion.code()
String VERSION_NAME = androidGitVersion.name()

println 'VersionName: ' + VERSION_NAME
println 'VersionCode: ' + VERSION_CODE

repositories {
    google()
    mavenCentral()
}

android {
    namespace "tech.akpmakes.android.taskkeeper"
    compileSdk 34
    signingConfigs {
        debugApp {
            if(project.hasProperty('ANDROID_SIGNING_KEY')) {
                keyAlias ANDROID_SIGNING_KEY
                keyPassword ANDROID_KEY_PASSWORD
                storeFile file(ANDROID_KEY_FILE)
                storePassword ANDROID_STORE_PASSWORD
            }
        }
    }
    defaultConfig {
        applicationId "tech.akpmakes.android.taskkeeper"
        minSdkVersion 19
        targetSdkVersion 34
        multiDexEnabled true
        versionName "Debug"
        versionCode 1
        vectorDrawables.useSupportLibrary = true
    }
    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            if(project.hasProperty('ANDROID_SIGNING_KEY')) {
                signingConfig signingConfigs.debugApp
            }
            // Disable crashlytics
            ext.enableCrashlytics = false
        }
    }
    flavorDimensions "buildType"
    productFlavors {
        dev {
            dimension "buildType"
            applicationIdSuffix ".debug"
            resourceConfigurations += ['en', 'xxhdpi']
        }
        prod {
            dimension "buildType"
            versionName VERSION_NAME
            versionCode VERSION_CODE
        }
    }


    buildFeatures {
        buildConfig = true
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation platform('com.google.firebase:firebase-bom:32.8.0')
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-database'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    // multidex support is needed until we're minSdk >= 21
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.firebaseui:firebase-ui-database:8.0.2'
    implementation 'com.google.android.gms:play-services-auth:21.0.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
}

apply plugin: 'com.google.gms.google-services'