plugins {
    id 'com.gladed.androidgitversion' version '0.3.4'
}

apply plugin: 'com.android.application'

androidGitVersion {
    baseCode 1900000000
    codeFormat = 'MNNPPBBB'
    hideBranches = [ ]
    untrackedIsDirty = true
    format = "%tag%%-count%%-commit%%-dirty%"
}

int VERSION_CODE = androidGitVersion.code()
String VERSION_NAME = androidGitVersion.name()

println 'VersionName: ' + VERSION_NAME
println 'VersionCode: ' + VERSION_CODE

repositories {
    jcenter()
    google()
    mavenCentral()
}

android {
    namespace "tech.akpmakes.android.taskkeeper"
    compileSdk 33
    signingConfigs {
        debugApp {
            if(project.hasProperty('ANDROID_SIGNING_KEY')) {
                keyAlias ANDROID_SIGNING_KEY
                keyPassword ANDROID_KEY_PASSWORD
                storeFile file(ANDROID_KEY_FILE)
                storePassword ANDROID_STORE_PASSWORD
            }
        }
    }
    defaultConfig {
        applicationId "tech.akpmakes.android.taskkeeper"
        minSdkVersion 15
        targetSdkVersion 33
        versionName "Debug"
        versionCode 1
        vectorDrawables.useSupportLibrary = true
    }
    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            if(project.hasProperty('ANDROID_SIGNING_KEY')) {
                signingConfig signingConfigs.debugApp
            }
            // Disable crashlytics
            ext.enableCrashlytics = false
        }
    }
    flavorDimensions "buildType"
    productFlavors {
        dev {
            dimension "buildType"
            applicationIdSuffix ".debug"
            resConfigs "en", "xxhdpi"
        }
        prod {
            dimension "buildType"
            versionName VERSION_NAME
            versionCode VERSION_CODE
        }
    }

    lintOptions {
      abortOnError false
    }

    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.firebase:firebase-auth:11.2.0'
    implementation 'com.google.firebase:firebase-database:11.2.0'
    implementation 'com.google.firebase:firebase-config:11.2.0'
    implementation 'com.firebaseui:firebase-ui-database:2.2.0'
    implementation 'com.google.android.gms:play-services-auth:11.2.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
}

apply plugin: 'com.google.gms.google-services'