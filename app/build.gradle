import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

def version = "1.0.0"

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

static def buildCode() {
    def df = new SimpleDateFormat("yyMMddHHmm")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return Integer.parseInt(df.format(new Date()))
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://maven.google.com' }
}

android {
    signingConfigs {
        release {
            keyAlias 'signingKey'
            keyPassword ANDROID_KEY_PASSWORD
            storeFile file(ANDROID_KEY_FILE)
            storePassword ANDROID_STORE_PASSWORD
        }
    }
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "tech.akpmakes.android.taskkeeper"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode buildCode()
        versionName "${version}-alpha-${buildTime()} (${buildCode()})"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    compile 'com.android.support:appcompat-v7:26.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.google.firebase:firebase-perf:11.0.4'
    compile 'com.google.firebase:firebase-auth:11.0.4'
    compile 'com.google.firebase:firebase-database:11.0.4'
    compile 'com.google.firebase:firebase-messaging:11.0.4'
    compile 'com.google.firebase:firebase-config:11.0.4'
    compile 'com.firebaseui:firebase-ui-database:2.1.1'
    compile 'com.google.android.gms:play-services-auth:11.0.4'
    compile 'com.android.support:design:26.0.0'
    compile 'com.android.support:support-v4:26.0.0'
    compile 'com.android.support:support-vector-drawable:26.0.0'
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'